![](D:\Java\笔记\图片\1-16【jvm】\3.png)

# 第一章 类文件结构

在Linux里面编写一个HelloWorld程序：

```java
public class HelloWorld {
	public static void main(String[] args) {
		System.out.println("hello world");
	}
}
```

执行编译 `javac -parameters -d . HelloWorld.java`，如果加`-parameters`这个参数，那么他就会保留方法中参数的名称信息（`比如 args`）。

Linux下用`od -t xC HelloWorld.class` 遍历查看二进制字节码文件内容：

```sh
[root@localhost ~]# od -t xC HelloWorld.class
0000000 ca fe ba be 00 00 00 34 00 23 0a 00 06 00 15 09
0000020 00 16 00 17 08 00 18 0a 00 19 00 1a 07 00 1b 07
0000040 00 1c 01 00 06 3c 69 6e 69 74 3e 01 00 03 28 29
0000060 56 01 00 04 43 6f 64 65 01 00 0f 4c 69 6e 65 4e
0000100 75 6d 62 65 72 54 61 62 6c 65 01 00 12 4c 6f 63
0000120 61 6c 56 61 72 69 61 62 6c 65 54 61 62 6c 65 01
0000140 00 04 74 68 69 73 01 00 1d 4c 63 6e 2f 69 74 63
0000160 61 73 74 2f 6a 76 6d 2f 74 35 2f 48 65 6c 6c 6f
0000200 57 6f 72 6c 64 3d 01 00 04 6d 61 69 6e 01 00 16
0000220 28 5b 4c 6a 61 76 61 2f 6c 61 6e 67 2f 53 74 72
0000240 69 6e 67 3b 29 56 01 00 04 61 72 67 73 01 00 13
0000260 5b 4c 6a 61 76 61 2f 6c 61 6e 67 2f 53 74 72 69
0000300 6e 67 3b 01 00 10 4d 65 74 68 6f 64 50 61 72 61
0000320 6d 65 74 65 72 73 01 00 0a 53 6f 75 72 63 65 46
0000340 69 6c 65 01 00 0f 48 65 6c 6c 6f 57 6f 72 6c 64
0000360 2e 6a 61 76 61 0c 00 07 00 08 07 00 1d 0c 00 1e
0000400 00 1f 01 00 0b 68 65 6c 6c 6f 20 77 6f 72 6c 64
0000420 07 00 20 0c 00 21 00 22 01 00 1b 63 6e 2f 69 74
0000440 63 61 73 74 2f 6a 76 6d 2f 74 35 2f 48 65 6c 6c
0000460 6f 57 6f 72 6c 64 01 00 10 6a 61 76 61 2f 6c 61
0000500 6e 67 2f 4f 62 6a 65 63 74 01 00 10 6a 61 76 61
0000520 2f 6c 61 6e 67 2f 53 79 73 74 65 6d 01 00 03 6f 
0000540 75 74 01 00 15 4c 6a 61 76 61 2f 69 6f 2f 50 72
0000560 69 6e 74 53 74 72 65 61 6d 3b 01 00 13 6a 61 76
0000600 61 2f 69 6f 2f 50 72 69 6e 74 53 74 72 65 61 6d
0000620 01 00 07 70 72 69 6e 74 6c 6e 01 00 15 28 4c 6a
0000640 61 76 61 2f 6c 61 6e 67 2f 53 74 72 69 6e 67 3b
0000660 29 56 00 21 00 05 00 06 00 00 00 00 00 02 00 01
0000700 00 07 00 08 00 01 00 09 00 00 00 2f 00 01 00 01
0000720 00 00 00 05 2a b7 00 01 b1 00 00 00 02 00 0a 00
0000740 00 00 06 00 01 00 00 00 04 00 0b 00 00 00 0c 00
0000760 01 00 00 00 05 00 0c 00 0d 00 00 00 09 00 0e 00
0001000 0f 00 02 00 09 00 00 00 37 00 02 00 01 00 00 00
0001020 09 b2 00 02 12 03 b6 00 04 b1 00 00 00 02 00 0a
0001040 00 00 00 0a 00 02 00 00 00 06 00 08 00 07 00 0b
0001060 00 00 00 0c 00 01 00 00 00 09 00 10 00 11 00 00 
0001100 00 12 00 00 00 05 01 00 10 00 00 00 01 00 13 00
0001120 00 00 02 00 14
```

根据 JVM 规范，类文件结构如下：

```apl
ClassFile {
	u4		magic;			    # 魔数
	u2		minor_version;		# 小版本号
	u2		major_version;		# 主版本号 
	u2		constant_pool_count; 						# 常量池信息
	cp_info		constant_pool[constant_pool_count-1];	  # 常量池信息
	u2 		access_flags;		# 访问修饰符
	u2		this_class;			# 这个类自己的包名、类名的信息
	u2		super_class;		# 父类的信息
	u2		interfaces_count;				# 接口信息
	u2		interfaces[interfaces_count];	 # 接口信息
	u2		fields_count;				# 类中的变量信息
	field_info	field[fields_count];	 # 类中的变量信息
	u2		methods_count;					# 类中的方法信息
	method_info	methods[methods_count];		  # 类中的方法信息
	u2		attributes_count;						# 类中的一些附加信息
	attribute_info	attributes[attributes_count];	  # 类中的一些附加信息
}
```

可以看到前面的u2 u4这样的是字节数，比如前四个字节（u4）是魔数magic，接下来的两个字节（u2）是小版本号minor_version，再接下来两个字节（u2）是主版本号major_version等等。

## 1.1 魔数

魔数：u4 magic，对应字节码文件的0~3个字节。下面的`ca fe ba be`就是了。

0000000 `ca fe ba be` 00 00 00 34 00 23 0a 00 06 00 15 09

魔数是指所有的文件都有自己的特定的类型，Java文件用了四个字节来表示这是一个Java文件，而不是其他的文件，不同的文件都有自己的魔数信息。魔术就是标识你这个文件到底是啥类型。

## 1.2 版本

小版本号：u2 minor_version; 主版本号：u2 major_version;。下面的`00 00 00 34` 就是了。

0000000 ca fe ba be `00 00 00 34` 00 23 0a 00 06 00 15 09

前两个`00 00`是minor version，没有在类文件中体现，主要是他的主版本即`00 34`（34是16进制，翻译成十进制就是52），52就代表JDK8，51是JDK7，53是JDK9。总之，编译后的class文件版本是34的话，就说明是基于JDK8的版本。

## 1.3 常量池

|        Contant Type         | Value |
| :-------------------------: | :---: |
|       CONSTANT_Class        |   7   |
|      CONSTANT_Fieldref      |   9   |
|     CONSTANT_Methodref      |  10   |
| CONSTANT_InterfaceMethodref |  11   |
|       CONSTANT_String       |   8   |
|      CONSTANT_Integer       |   3   |
|       CONSTANT_Float        |   4   |
|        CONSTANT_Long        |   5   |
|       CONSTANT_Double       |   6   |
|    CONSTANT_NameAndType     |  12   |
|        CONSTANT_Utf8        |   1   |
|    CONSTANT_MethodHandle    |  15   |
|     CONSTANT_MethodType     |  16   |
|   CONSTANT_InvokeDynamic    |  18   |

0000000 ca fe ba be 00 00 00 34 `00 23` 0a 00 06 00 15 09

常量池占据了整个类文件中的相当大的比重。8 ~ 9字节，即`00 23`表示常量池长度，`00 23`（35）表示常量池有 `#1~#34`项（一共有35项），注意`#0`项不计入（所以有34项），也没有值。

这些字节第一个代表的是类型，将十六进制转换为十进制通过上面的表可以查出来到底代表什么类型。

详细如下：

* **第 #1 项**
  `0a` 表示一个method信息，`00 06`和`00 15`（21）表示引用了常量池中`#6`和`#21`项来获得这个方法的【所属类】和【方法名】

  0000000 ca fe ba be 00 00 00 34 00 23 `0a` `00 06 00 15` 09

  

* **第 #2 项**
  `09` 表示一个filed信息，`00 16`（22）和`00 17`（23）表示他引用了常量池中`#22`和`#23`项来获得这个成员变量的【所属类】和【成员变量名】

  0000000 ca fe ba be 00 00 00 34 00 23 0a 00 06 00 15 `09`
  0000020 `00 16 00 17` 08 00 18 0a 00 19 00 1a 07 00 1b 07

  

* **第 #3 项**
  `08` 表示一个字符串常量名称，`00 18`（24）表示它引用了常量池中 `#24`项

  0000020 00 16 00 17 `08 00 18` 0a 00 19 00 1a 07 00 1b 07

  

* **第 #4 项**
  `0a` 表示一个method信息，`00 19`（25）和`00 1a`（26）表示它引用了常量池中`#25`和`#26`项来获得这个方法的【所属类】和【方法名】

  0000020 00 16 00 17 08 00 18 `0a 00 19 00 1a` 07 00 1b 07

  

* **第 #5 项**
  `07` 表示一个class信息，`00 1b`（27）表示它引用了常量池中`#27`项

  0000020 00 16 00 17 08 00 18 0a 00 19 00 1a `07 00 1b` 07

  

* **第 #6 项**
  `07`表示一个class信息，`00 1c`（28）表示它引用了常量池中`#28`项

  0000020 00 16 00 17 08 00 18 0a 00 19 00 1a 07 00 1b `07`
  0000040 `00 1c` 01 00 06 3c 69 6e 69 74 3e 01 00 03 28 29

  

* **第 #07 项**
  `01`表示一个utf8串，`00 06` 表示长度，`3c 69 63 69 74 3e`是【< init >】

  0000040 00 1c `01 00 06 3c 69 6e 69 74 3e` 01 00 03 28 29

  

* **第 #08 项**
  `01`表示一个utf8串，`00 03` 表示长度，`28 29 56` 是【()V】其实就是表示无参、无返回值

  0000040 00 1c 01 00 06 3c 69 6e 69 74 3e `01 00 03 28 29`
  0000060 `56` 01 00 04 43 6f 64 65 01 00 0f 4c 69 6e 65 4e

  

* **第 #09 项**
  `01`表示一个utf8串，`00 04`表示长度，`43 6f 64 65`是【Code（方法的一个属性，对应着字节码）】

  0000060 56 `01 00 04 43 6f 64 65` 01 00 0f 4c 69 6e 65 4e

  

* **第 #10 项**
  `01` 表示一个utf8串，`00 0f`（15）表示长度，`4c 69 6e 65 4e 75 6d 62 72 54 61 62 6c 65`是【LineNumberTable（方法的属性，方法的行号表）】

  0000060 56 01 00 04 43 6f 64 65 `01 00 0f 4c 69 6e 65 4e`
  0000100 `75 6d 62 65 72 54 61 62 6c 65` 01 00 12 4c 6f 63

  

* **第 #11 项**
  `01`表示一个utf8串，`00 12`（18）表示长度，`4c ~ 65` 是【LocalVariableTable（代表方法的一个属性，是方法的本地变量表）】

  0000100 75 6d 62 65 72 54 61 62 6c 65 `01 00 12 4c 6f 63`
  0000120 `61 6c 56 61 72 69 61 62 6c 65` 54 61 62 6c 65 01

  

* **第 #12 项**
  `01` 表示 utf8串，`00 04`表示长度，`74 68 69 73`是【this】

  0000120 61 6c 56 61 72 69 61 62 6c 65 54 61 62 6c 65 `01`
  0000140 `00 04 74 68 69 73` 01 00 1d 4c 63 6e 2f 69 74 63

  

* **第 #13 项**
  `01`表示utf8串，`00 1d`（29）表示长度，是【Lcom/waca/jvm/t5/HelloWorld;（比如）（这是类型，在字节码里表示类型的话，引用类型是L开头;结尾）】

  0000140 00 04 74 68 69 73 `01 00 1d 4c 63 6e 2f 69 74 63`
  0000160 `61 73 74 2f 6a 76 6d 2f 74 35 2f 48 65 6c 6c 6f`
  0000200 `57 6f 72 6c 64 3d` 01 00 04 6d 61 69 6e 01 00 16

  

* **第 #14 项**
  `01`表示 utf8串，`00 04`表示长度，`74 68 69 73` 是【main（方法名称，可以和`#13`联系起来，是表示哪个类中的main方法）】

  0000200 57 6f 72 6c 64 3d `01 00 04 6d 61 69 6e` 01 00 16

  

* **第 #15 项**
  `01`表示一个utf8串，`00 16`（22）表示长度，是【（[Ljava/lang/String;）V】括号里面是参数类型，前面再加一个左半边的[，表示他是数组，即其实就是参数为字符串数组，无返回值。

  0000200 57 6f 72 6c 64 3d 01 00 04 6d 61 69 6e `01 00 16`
  0000220 `28 5b 4c 6a 61 76 61 2f 6c 61 6e 67 2f 53 74 72`
  0000240 `69 6e 67 3b 29 56` 01 00 04 61 72 67 73 01 00 13

  

* **第 #16 项**
  `01`表示一个utf8串，`00 04`表示长度，是【args(刚才main方法的参数名称)】

  0000240 69 6e 67 3b 29 56 `01 00 04 61 72 67 73` 01 00 13

  

* **第 #17 项**
  `01`表示一个utf8串，`00 13`（19）表示长度，是【[Ljava/lang/String;（进一步描述了刚才的参数类型）】

  0000240 69 6e 67 3b 29 56 01 00 04 61 72 67 73 `01 00 13`
  0000260 `5b 4c 6a 61 76 61 2f 6c 61 6e 67 2f 53 74 72 69`
  0000300 `6e 67 3b` 01 00 10 4d 65 74 68 6f 64 50 61 72 61

  

* **第 #18 项**
  `01` 表示utf8串，`00 10`（16）表示长度，是【MethodParameters（方法的一个属性，是指方法的参数名称信息）】

  0000300 6e 67 3b `01 00 10 4d 65 74 68 6f 64 50 61 72 61`
  0000320 `6d 65 74 65 72 73` 01 00 0a 53 6f 75 72 63 65 46

  

* **第 #19 项**
  `01`表示一个utf8串，`00 0a`（10）表示长度，是【SourceFile】

  0000320 6d 65 74 65 72 73 `01 00 0a 53 6f 75 72 63 65 46`
  0000340 `69 6c 65` 01 00 0f 48 65 6c 6c 6f 57 6f 72 6c 64

  

* **第 #20 项**
  `01`表示一个utf8串，`00 0f`（15）表示长度，是【HelloWorld.java（#19 #20联系起来就是说这个类的源文件是HelloWorld.java）】

  0000340 69 6c 65 `01 00 0f 48 65 6c 6c 6f 57 6f 72 6c 64`
  0000360 `2e 6a 61 76 61` 0c 00 07 00 08 07 00 1d 0c 00 1e

  

* **第 #21 项**
  `0c`（12）表示一个【名+类型】，`00 07 00 08`引用了常量池中`#7 #8`两项。

  0000360 2e 6a 61 76 61 `0c 00 07 00 08` 07 00 1d 0c 00 1e

  

* **第 #22 项**
  `07` 表示一个class信息，`00 1d`（29）引用了常量池中`#29`项

  0000360 2e 6a 61 76 61 0c 00 07 00 08 `07 00 1d` 0c 00 1e

  

* **第 #23 项**
  `0c`表示一个【名+类型】，`00 1e`（30）`00 1f`（31）引用了常量池中`#30 #31`两项。

  0000360 2e 6a 61 76 61 0c 00 07 00 08 07 00 1d `0c 00 1e`
  0000400 `00 1f` 01 00 0b 68 65 6c 6c 6f 20 77 6f 72 6c 64

  

* **第 #24 项**
  `01`表示一个utf8串，`00 0b`（11）表示长度，是hello world

  0000400 00 1f `01 00 0b 68 65 6c 6c 6f 20 77 6f 72 6c 64`

  

* **第 #25 项**
  `07`表示一个class信息，`00 20`（32）引用了常量池中`#32`项

  0000420 `07 00 20` 0c 00 21 00 22 01 00 1b 63 6e 2f 69 74

  

* **第 #26 项**
  `0c`表示【名+类型】，`00 21`（33）`00 22`（34）引用了常量池中`#33 #34`两项

  0000420 07 00 20 `0c 00 21 00 22` 01 00 1b 63 6e 2f 69 74

  

* **第 #27 项**
  `01`表示一个utf8串，`00 1b`（27）表示长度，是【com/linxuan/jvm/HelloWorld】

  0000420 07 00 20 0c 00 21 00 22 `01 00 1b 63 6e 2f 69 74`
  0000440 `63 61 73 74 2f 6a 76 6d 2f 74 35 2f 48 65 6c 6c`
  0000460 `6f 57 6f 72 6c 64` 01 00 10 6a 61 76 61 2f 6c 61

  

* **第 #28 项**
  `01` 表示一个utf8串，`00 10`（16）表示长度（6a开始到74，即就是Object类），是【java/lang/Object】

  0000460 6f 57 6f 72 6c 64 `01 00 10 6a 61 76 61 2f 6c 61`
  0000500 `6e 67 2f 4f 62 6a 65 63 74` 01 00 10 6a 61 76 61

  

* **第 #29 项**
  `01` 表示一个utf8串，`00 10`（16）表示长度，即6a到6d，是java/lang/System

  0000500 6e 67 2f 4f 62 6a 65 63 74 `01 00 10 6a 61 76 61`
  0000520 `2f 6c 61 6e 67 2f 53 79 73 74 65 6d` 01 00 03 6f

  

* **第 #30 项**
  `01` 表示一个utf8串，`00 03`表示长度，是out

  0000520 2f 6c 61 6e 67 2f 53 79 73 74 65 6d `01 00 03 6f`
  0000540 `75 74` 01 00 15 4c 6a 61 76 61 2f 69 6f 2f 50 72

  

* **第 #31 项**
  `01`表示一个utf8串，`00 15`（21）表示长度，即4c到3b，是Ljava/io/PrintStream;

  0000540 75 74 `01 00 15 4c 6a 61 76 61 2f 69 6f 2f 50 72`
  0000560 `69 6e 74 53 74 72 65 61 6d 3b` 01 00 13 6a 61 76

  

* **第 #32 项**
  `01` 表示一个utf8串，`00 13`（19）表示长度，是java/io/PrintStream

  0000560 69 6e 74 53 74 72 65 61 6d 3b `01 00 13 6a 61 76`
  0000600 `61 2f 69 6f 2f 50 72 69 6e 74 53 74 72 65 61 6d`

  

* **第 #33 项**
  `01` 表示一个utf8串，`00 07`表示长度，是【println】

  0000620 `01 00 07 70 72 69 6e 74 6c 6e` 01 00 15 28 4c 6a

  

* **第 #34 项**
  `01` 是utf8串，`00 15`（21）是长度，是【(Ljava/lang/String;)V】，即类型是String类型，返回值是void

  0000620 01 00 07 70 72 69 6e 74 6c 6e `01 00 15 28 4c 6a`
  0000640 `61 76 61 2f 6c 61 6e 67 2f 53 74 72 69 6e 67 3b`
  0000660 `29 56` 00 21 00 05 00 06 00 00 00 00 00 02 00 01

通过这个小例子，可以看得出常量池里就是记录着我们Java类中的各种信息，包括它的类的信息、父类的信息、方法的信息、成员变量的信息等等，还有方法的属性。

## 1.4 访问标识和继承信息

上面的是类文件结构中的魔数、版本、常量池，接下来是access_flags，这是访问修饰的描述。从`00`开始是`access_flag`。

0000660 29 56 `00 21 00 05 00 06 00 00` 00 00 00 02 00 01

- `00 21`表示该class是一个类，公共的（看下表21是由哪几项加起来的，比如表中0x0020加0x0001就是21，表示他是一个公共的类）。
- `00 05`表示根据常量池中去第`#5`项找到本类全限定名。
- `00 06`表示根据常量池中去第`#6`项找到父类全限定名。
- `00 00`表示接口的数量，本类为0。

| FlagName       | Value  | Interpretation                                               |
| -------------- | ------ | ------------------------------------------------------------ |
| ACC_PUBLIC     | 0x0001 | Declared public;may be accessed from outside its             |
| ACC_FINAL      | 0x0010 | Declared final;no subclasses allowed                         |
| ACC_SUPER      | 0x0020 | Treat superclass methods specially when invoked by the invokespecial instruction（表示类） |
| ACC_INTERFACE  | 0x0200 | Is an interface,not a class                                  |
| ACC_ABSTRACT   | 0x0400 | Declared abstract;must not be instantiated                   |
| ACC_SYNTHETIC  | 0x1000 | Declared synthetic;not present in the source code（人工合成的，不是源代码的） |
| ACC_ANNOTATION | 0x2000 | Declared as an annotation type（注解）                       |
| ACC_ENUM       | 0x4000 | Declared as an enum type（枚举）                             |

## 1.5 field 信息

接下来就是 `fields_count`和`fields[fields_count]`，即成员变量的信息。

0000660 29 56 00 21 00 05 00 06 00 00 `00 00` 00 02 00 01

这里的`00 00`表示成员变量数量是0。

| FieldType    | Type      | Interpretation                                               |
| ------------ | --------- | ------------------------------------------------------------ |
| B            | byte      | signed byte                                                  |
| C            | char      | Unicode character code point in the Basic Multilingual Plane,encoded with UTF-16 |
| D            | double    | double-precision floating-point value                        |
| F            | float     | single-precision floating-poiint value                       |
| I            | int       | integer                                                      |
| J            | long      | long integer                                                 |
| L ClassName; | reference | an instance of class ClassName                               |
| S            | short     | signed short                                                 |
| Z            | boolean   | true or false                                                |
| [            | reference | one array dimension（一维数组）                              |

## 1.6 method信息

0000660 29 56 00 21 00 05 00 06 00 00 00 00 `00 02` 00 01

从`00 02`两个字节开始，这是代表本类中方法的数量，`02`说明有两个方法，第一个其实就是那个构造方法，第二个是main方法。一个方法由访问修饰符、名称、参数描述、方法属性数量、方法属性组成。

### 1.6.1 构造方法详解

0000660 29 56 00 21 00 05 00 06 00 00 00 00 00 02 <font color = "red">00 01</font>
0000700 <font color = "blue">00 07</font> <font color = "green">00 08</font> <font color = "yellOw">00 01</font> <font color = "red">00 09 00 00 00 2f 00 01 00 01</font>
0000720 <font color = "red">00 00 00 05 2a b7 00 01 b1 00 00 00 02 00 0a 00</font>
0000740 <font color = "red">00 00 06 00 01 00 00 00 04 00 0b 00 00 00 0c 00</font>
0000760 <font color = "red">01 00 00 00 05 00 0c 00 0d 00 00 00 09 00 0e 00</font>

- 红色`00 01`代表访问修饰符public，查表就可以得到。

- 蓝色`00 07`代表引用了常量池 `#07` 项作为方法名称，就是init构造方法。

- 绿色`00 08`代表引用了常量池 `#08` 项作为方法参数描述，是【()V】其实就是表示无参、无返回值。

- 黄色`00 01`代表方法属性数量，本方法是1。

- 红色代表方法属性

  `00 09` 表示引用了常量池 `#09` 项，发现是【Code】属性

  `00 00 00 2f` 表示此属性的长度是 47

  `00 01` 表示【操作数栈】最大深度

  `00 01` 表示【局部变量表】最大槽（slot）数

  `00 00 00 05` 表示字节码长度，本例是 5

  `2a b7 00 01 b1` 是字节码指令

  `00 00 00 02` 表示方法细节属性数量，本例是 2

  `00 0a` 表示引用了常量池 `#10` 项，发现是【LineNumberTable】属性

  * `00 00 00 06` 表示此属性的总长度，本例是 6
  * `00 01` 表示【LineNumberTable】长度
  * `00 00` 表示【字节码】行号 00 04 表示【java 源码】行号

  `00 0b` 表示引用了常量池 #11 项，发现是【LocalVariableTable】属性

  * `00 00 00 0c` 表示此属性的总长度，本例是 12
  * `00 01` 表示【LocalVariableTable】长度
  * `00 00` 表示局部变量生命周期开始，相对于字节码的偏移量
  * `00 05` 表示局部变量覆盖的范围长度
  * `00 0c` 表示局部变量名称，本例引用了常量池 #12 项，是【this】
  * `00 0d` 表示局部变量的类型，本例引用了常量池 #13 项，是【Lcn/itcast/jvm/t5/HelloWorld;】
  * `00 00` 表示局部变量占有的槽位（slot）编号，本例是 0

### 1.6.2 主方法详解

0000760 01 00 00 00 05 00 0c 00 0d 00 00 <font color = "red">00 09</font> <font color = "blue">00 0e</font> <font color = "green">00</font>
0001000 <font color = "green">0f</font> <font color = "yellow">00 02</font> <font color = "red">00 09 00 00 00 37 00 02 00 01 00 00 00</font>
0001020 <font color = "red">09 b2 00 02 12 03 b6 00 04 b1 00 00 00 02 00 0a</font>
0001040 <font color = "red">00 00 00 0a 00 02 00 00 00 06 00 08 00 07 00 0b</font>
0001060 <font color = "red">00 00 00 0c 00 01 00 00 00 09 00 10 00 11 00 00</font>

- 红色代表访问修饰符（本类中是 public static）

- 蓝色代表引用了常量池 #14 项作为方法名称

- 绿色代表引用了常量池 #15 项作为方法参数描述

- 黄色代表方法属性数量，本方法是 2

- 红色代表方法属性（属性1）

  00 09 表示引用了常量池 #09 项，发现是【Code】属性

  00 00 00 37 表示此属性的长度是 55

  00 02 表示【操作数栈】最大深度

  00 01 表示【局部变量表】最大槽（slot）数

  00 00 00 05 表示字节码长度，本例是 9

  b2 00 02 12 03 b6 00 04 b1 是字节码指令

  00 00 00 02 表示方法细节属性数量，本例是 2

  00 0a 表示引用了常量池 #10 项，发现是【LineNumberTable】属性

  * 00 00 00 0a 表示此属性的总长度，本例是 10
  * 00 02 表示【LineNumberTable】长度
  * 00 00 表示【字节码】行号 00 06 表示【java 源码】行号
  * 00 08 表示【字节码】行号 00 07 表示【java 源码】行号

  00 0b 表示引用了常量池 #11 项，发现是【LocalVariableTable】属性

  * 00 00 00 0c 表示此属性的总长度，本例是 12
  * 00 01 表示【LocalVariableTable】长度
  * 00 00 表示局部变量生命周期开始，相对于字节码的偏移量
  * 00 09 表示局部变量覆盖的范围长度
  * 00 10 表示局部变量名称，本例引用了常量池 #16 项，是【args】
  * 00 11 表示局部变量的类型，本例引用了常量池 #17 项，是【[Ljava/lang/String;】
  * 00 00 表示局部变量占有的槽位（slot）编号，本例是 0



0001100 <font color = "red">00 12 00 00 00 05 01 00 10 00 00</font> 00 01 00 13 00
0001120 00 00 02 00 14

红色代表方法属性（属性2）

* 00 12 表示引用了常量池 #18 项，发现是【MethodParameters】属性

  - 00 00 00 05 表示此属性的总长度，本例是 5

  - 01 参数数量

  - 00 10 表示引用了常量池 #16 项，是【args】

  - 00 00 访问修饰符

## 1.7 附加属性

0001100 00 12 00 00 00 05 01 00 10 00 00 <font color = "red">00 01 00 13 00</font>
0001120 <font color = "red">00 00 02 00 14</font>

00 01 表示附加属性数量

00 13 表示引用了常量池 #19 项，即【SourceFile】

00 00 00 02 表示此属性的长度

00 14 表示引用了常量池 #20 项，即【HelloWorld.java】

https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html

通过这个文档都能查询所有的字节描述的含义。

# 第二章 字节码指令

可参考：https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-6.html#jvms-6.5

接着上一节研究一下两组字节码指令，一个是 `public cn.linxuan.jvm.HelloWorld();` 构造方法的字节码指令：

```assembly
2a b7 00 01 b1
```

1. 2a => aload_0 加载 slot 0 的局部变量，即 this，做为下面的 invokespecial 构造方法调用的参数
2. b7 => invokespecial 预备调用构造方法，哪个方法呢？
3. 00 01 引用常量池中 #1 项，即【 Method java/lang/Object."<init>":()V 】
4. b1 表示返回

另一个是 `public static void main(java.lang.String[]);` 主方法的字节码指令：

```assembly
b2 00 02 12 03 b6 00 04 b1
```

1. b2 => getstatic 用来加载静态变量，哪个静态变量呢？
2. 00 02 引用常量池中 #2 项，即【Field java/lang/System.out:Ljava/io/PrintStream;】
3. 12 => ldc 加载参数，哪个参数呢？
4. 03 引用常量池中 #3 项，即 【String hello world】
5. b6 => invokevirtual 预备调用成员方法，哪个方法呢？
6. 00 04 引用常量池中 #4 项，即【Method java/io/PrintStream.println:(Ljava/lang/String;)V】
7. b1 表示返回

## 2.1 javap工具

Oracle 提供了 **javap** 工具来反编译 class 文件

```java
[root@localhost ~]# javap -v HelloWorld.class
Classfile /root/HelloWorld.class
  Last modified 2020-6-6; size 434 bytes
  MD5 checksum df1dce65bf6fb0b4c1de318051f4a67e
  Compiled from "Demo1.java"
public class com.linxuan.JVM.day5.Demo1
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #6.#15         // java/lang/Object."<init>":()V
   #2 = Fieldref           #16.#17        // java/lang/System.out:Ljava/io/PrintStream;
   #3 = String             #18            // hello world
   #4 = Methodref          #19.#20        // java/io/PrintStream.println:(Ljava/lang/String;)V
   #5 = Class              #21            // com/nyima/JVM/day5/Demo1
   #6 = Class              #22            // java/lang/Object
   #7 = Utf8               <init>
   #8 = Utf8               ()V
   #9 = Utf8               Code
  #10 = Utf8               LineNumberTable
  #11 = Utf8               main
  #12 = Utf8               ([Ljava/lang/String;)V
  #13 = Utf8               SourceFile
  #14 = Utf8               Demo1.java
  #15 = NameAndType        #7:#8          // "<init>":()V
  #16 = Class              #23            // java/lang/System
  #17 = NameAndType        #24:#25        // out:Ljava/io/PrintStream;
  #18 = Utf8               hello world
  #19 = Class              #26            // java/io/PrintStream
  #20 = NameAndType        #27:#28        // println:(Ljava/lang/String;)V
  #21 = Utf8               com/linxuan/JVM/day5/Demo1
  #22 = Utf8               java/lang/Object
  #23 = Utf8               java/lang/System
  #24 = Utf8               out
  #25 = Utf8               Ljava/io/PrintStream;
  #26 = Utf8               java/io/PrintStream
  #27 = Utf8               println
  #28 = Utf8               (Ljava/lang/String;)V
{
  public com.nyima.JVM.day5.Demo1();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:	// 字节码行号对应源代码行号
        line 7: 0

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: getstatic     #2           // Field java/lang/System.out:Ljava/io/PrintStream;
         3: ldc           #3           // String hello world
         5: invokevirtual #4           // Method java/io/PrintStream.println:(Ljava/lang/String;)V

         8: return
      LineNumberTable:
        line 9: 0		// 源代码第9行对应字节码0行。
        line 10: 8
}
```



## 2.2 图解方法执行流程

```java
public class Demo01 {
	public static void main(String[] args) {
		int a = 10;        
		int b = Short.MAX_VALUE + 1;        
		int c = a + b;        
		System.out.println(c);   
	}
}
```

```java
// 编译后：
Classfile /D:/Java/vscode-java/MavenProjects/jvm/target/classes/com/linxuan/Demo01.class
  Last modified 2022-8-23; size 583 bytes
  MD5 checksum ced08233f346a6db40f417d09ecd17fe
  Compiled from "Demo01.java"
public class com.linxuan.Demo01
  minor version: 0
  major version: 51
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Class              #2             // com/linxuan/Demo01
   #2 = Utf8               com/linxuan/Demo01
   #3 = Class              #4             // java/lang/Object
   #4 = Utf8               java/lang/Object
   #5 = Utf8               <init>
   #6 = Utf8               ()V
   #7 = Utf8               Code
   #8 = Methodref          #3.#9          // java/lang/Object."<init>":()V
   #9 = NameAndType        #5:#6          // "<init>":()V
  #10 = Utf8               LineNumberTable
  #11 = Utf8               LocalVariableTable
  #12 = Utf8               this
  #13 = Utf8               Lcom/linxuan/Demo01;
  #14 = Utf8               main
  #15 = Utf8               ([Ljava/lang/String;)V
  #16 = Integer            32768
  #17 = Fieldref           #18.#20        // java/lang/System.out:Ljava/io/PrintStream;
  #18 = Class              #19            // java/lang/System
  #19 = Utf8               java/lang/System
  #20 = NameAndType        #21:#22        // out:Ljava/io/PrintStream;
  #21 = Utf8               out
  #22 = Utf8               Ljava/io/PrintStream;
  #23 = Methodref          #24.#26        // java/io/PrintStream.println:(I)V
  #24 = Class              #25            // java/io/PrintStream
  #25 = Utf8               java/io/PrintStream
  #26 = NameAndType        #27:#28        // println:(I)V
  #27 = Utf8               println
  #28 = Utf8               (I)V
  #29 = Utf8               args
  #30 = Utf8               [Ljava/lang/String;
  #31 = Utf8               a
  #32 = Utf8               I
  #33 = Utf8               b
  #34 = Utf8               c
  #35 = Utf8               SourceFile
  #36 = Utf8               Demo01.java
{
  public com.linxuan.Demo01();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #8                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lcom/linxuan/Demo01;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=4, args_size=1
         0: bipush        10
         2: istore_1
         3: ldc           #16                 // int 32768
         5: istore_2
         6: iload_1
         7: iload_2
         8: iadd
         9: istore_3
        10: getstatic     #17                 // Field java/lang/System.out:Ljava/io/PrintStream;
        13: iload_3
        14: invokevirtual #23                 // Method java/io/PrintStream.println:(I)V
        17: return
      LineNumberTable:
        line 6: 0
        line 7: 3
        line 8: 6
        line 9: 10
        line 10: 17
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      18     0  args   [Ljava/lang/String;
            3      15     1     a   I
            6      12     2     b   I
           10       8     3     c   I
}
SourceFile: "Demo01.java"
```

步骤如下：

类加载器将main方法所在的类进行类加载操作，所谓类加载实际上就是把二进制字节码数据读取到内存中。二进制字节码就是上面的所有数据了。

* **常量池载入运行时常量池**

  常量池也属于方法区，只不过这里单独提出来了

  `#16  = Integer 32768`，Short范围内的数字是和字节码指令存放在一起的，大于这个范围的时候就会存放在常量池里面了。

  ![](D:\Java\笔记\图片\1-16【jvm】\14-1.png)

  

* **方法字节码载入方法区**

  （stack=2，locals=4） 对应操作数栈有2个空间（每个空间4个字节），局部变量表中有4个槽位

  ```java
    public static void main(java.lang.String[]);
      descriptor: ([Ljava/lang/String;)V
      flags: ACC_PUBLIC, ACC_STATIC
      Code:
        stack=2, locals=4, args_size=1
           0: bipush        10
           2: istore_1
           3: ldc           #16                 // int 32768
           5: istore_2
           6: iload_1
           7: iload_2
           8: iadd
           9: istore_3
          10: getstatic     #17                 // Field java/lang/System.out:Ljava/io/PrintStream;
          13: iload_3
          14: invokevirtual #23                 // Method java/io/PrintStream.println:(I)V
          17: return
  ```

  ![](D:\Java\笔记\图片\1-16【jvm】\14-2.png)

  

* **执行引擎开始执行字节码**

  `bipush 10`：将一个 byte 压入操作数栈 （操作数栈的宽度是4个字节，byte是1个字节，所以其长度会用0或1补齐 4 个字节），类似的指令还有

  - `sipush` 将一个 short 压入操作数栈（其长度会补齐 4 个字节）
  - `ldc` 将一个 int 压入操作数栈
  - `ldc2_w` 将一个 long 压入操作数栈（因为 long 是 8 个字节 所以分两次压入）
  - 这里小的数字都是和字节码指令存在一起，超过 short 范围的数字存入了常量池

  ![](D:\Java\笔记\图片\1-16【jvm】\14-3.png)

  

  `istore_1`：将操作数栈栈顶元素弹出，放入局部变量表的slot 1中，后面的1就代表存入到哪一个槽位。

  对应Java代码中的`a = 10`。

  ![](D:\Java\笔记\图片\1-16【jvm】\14-4.png)

  ![](D:\Java\笔记\图片\1-16【jvm】\14-5.png)

  

* `ldc #16`：读取运行时常量池中#16，即32768(超过short最大值范围的数会被放到运行时常量池中)，将其加载到操作数栈中

  Short.MAX_VALUE 是 32767，所以 32768 = Short.MAX_VALUE + 1 实际是在编译期间计算好的

  ![](D:\Java\笔记\图片\1-16【jvm】\14-6.png)

  

* `istore 2`：将操作数栈中的元素弹出，放到局部变量表的2号位置

  ![](D:\Java\笔记\图片\1-16【jvm】\14-7.png)

  ![](D:\Java\笔记\图片\1-16【jvm】\14-8.png)

  

* `iload1 iload2`：将局部变量表中1号位置和2号位置的元素放入操作数栈中。因为只能在操作数栈中执行运算操作

  ![](D:\Java\笔记\图片\1-16【jvm】\14-9.png)

  ![](D:\Java\笔记\图片\1-16【jvm】\14-10.png)

  

* `iadd`：将操作数栈中的两个元素弹出栈并相加，结果再压入操作数栈中

  ![](D:\Java\笔记\图片\1-16【jvm】\14-11.png)
  
  ![](D:\Java\笔记\图片\1-16【jvm】\14-12.png)
  
  
  
* `istore 3`：将操作数栈中的元素弹出，放入局部变量表的3号位置

  ![](D:\Java\笔记\图片\1-16【jvm】\14-13.png)

  ![](D:\Java\笔记\图片\1-16【jvm】\14-14.png)

  

* `getstatic #17`：在运行时常量池中找到#17，发现是一个对象。在堆内存中找到该对象，并将其**引用**放入操作数栈中

  ![](D:\Java\笔记\图片\1-16【jvm】\14-15.png)

  ![](D:\Java\笔记\图片\1-16【jvm】\14-16.png)

  

* `iload_3`：将局部变量表中3号位置的元素压入操作数栈中

  ![](D:\Java\笔记\图片\1-16【jvm】\14-17.png)

  

* `invokevirtual #23`：找到常量池 #23 项，定位到方法区 java/io/PrintStream.println:(I)V 方法。生成新的栈帧（分配 locals、stack等），传递参数，执行新栈帧中的字节码。

  ![](D:\Java\笔记\图片\1-16【jvm】\14-18.png)

  执行完毕，弹出栈帧

  清除 main 操作数栈内容

  ![](D:\Java\笔记\图片\1-16【jvm】\14-19.png)

  

* `return`：完成 main 方法调用，弹出 main 栈帧，程序结束

## 2.3 分析i++与++i

从字节码角度分析 i++ 相关题目

- i++：先赋值后操作，先执行iload再执行iinc；
- ++i：先操作后赋值

```java
package cn.itcast.jvm.t3.bytecode;
/**
* 从字节码角度分析 a++ 相关题目
*/
public class Demo3_2 {
    public static void main(String[] args) {
        int a = 10;
        int b = a++ + ++a + a--;
        System.out.println(a);
        System.out.println(b);
    }
}
```

```java
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=1
         0: bipush        10
         2: istore_1
         3: iload_1
         4: iinc          1, 1
         7: iinc          1, 1
        10: iload_1
        11: iadd
        12: iload_1
        13: iinc          1, -1
        16: iadd
        17: istore_2
        18: getstatic     #16                 // Field java/lang/System.out:Ljava/io/PrintStream;
        21: iload_1
        22: invokevirtual #22                 // Method java/io/PrintStream.println:(I)V
        25: getstatic     #16                 // Field java/lang/System.out:Ljava/io/PrintStream;
        28: iload_2
        29: invokevirtual #22                 // Method java/io/PrintStream.println:(I)V
        32: return
      LineNumberTable:
        line 6: 0
        line 7: 3
        line 8: 18
        line 9: 25
        line 10: 32
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      33     0  args   [Ljava/lang/String;
            3      30     1     a   I
           18      15     2     b   I
```

分析：

- 注意 iinc 指令是直接在局部变量 slot 上进行运算
- a++ 和 ++a 的区别是先执行 iload 还是 先执行 iinc。a++是先执行iload再执行iinc，而++a相反。

![](D:\Java\笔记\图片\1-16【jvm】\15-1.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-2.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-3.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-4.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-5.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-6.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-7.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-8.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-9.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-10.png)

![](D:\Java\笔记\图片\1-16【jvm】\15-11.png)

## 2.4 条件判断指令

| 二进制 |  字节码   |       含义       |
| :----: | :-------: | :--------------: |
|  0x99  |   ifeq    |  判断是否 == 0   |
|  0x9a  |   ifne    |   判断是否 !=0   |
|  0x9b  |   iflt    |   判断是否 < 0   |
|  0x9c  |   ifge    |  判断是否 >= 0   |
|  0x9d  |   ifgt    |   判断是否 > 0   |
|  0x9e  |   ifle    |  判断是否 <= 0   |
|  0x9f  | if_icmpeq |  两个int是否 ==  |
|  0xa0  | if_icmpne |  两个int是否 !=  |
|  0xa1  | if_icmplt |  两个int是否 <   |
|  0xa2  | if_icmpge |  两个int是否 >=  |
|  0xa3  | if_icmpgt |  两个int是否 >   |
|  0xa4  | if_icmple |  两个int是否 <=  |
|  0xa5  | if_acmpeq | 两个引用是否 ==  |
|  0xa6  | if_acmpne | 两个引用是否 !=  |
|  0xc6  |  ifnull   | 判断是否 == null |
|  0xc7  | ifnonnull | 判断是否 != null |

几点说明：

- byte，short，char 都会按 int 比较，因为操作数栈都是 4 字节
- goto 用来进行跳转到指定行号的字节码

```java
public class Demo3_3 {
    public static void main(String[] args) {
        int a = 0;
        if(a == 0) {
            a = 10;
        } else {
            a = 20;
        }
    }
}
```

```java
// 部分字节码如下：
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=2, args_size=1
         0: iconst_0			// 比较小的数都会用const来保存 -1~5 准备一个0放到操作数栈
         1: istore_1			// 放到局部变量表槽1=a中， 这两行等于a = 0 
         2: iload_1
         3: ifne          12
         6: bipush        10
         8: istore_1
         9: goto          15
        12: bipush        20
        14: istore_1
```

## 2.5 循环控制指令

其实循环控制还是前面介绍的那些指令，例如 while 循环：

```java
public class Demo3_4 {
    public static void main(String[] args) {
        int a = 0;
        while (a < 10) {
            a++;
        }
    }
}
```

字节码是：

```java
    0: iconst_0
    1: istore_1
    2: iload_1
    3: bipush 10
    5: if_icmpge 14
    8: iinc 1, 1
    11: goto 2
    14: return
```

再比如 do while 循环：

```java
public class Demo3_5 {
    public static void main(String[] args) {
        int a = 0;
        do {
            a++;
        } while (a < 10);
    }
}
```

字节码是：

```java
     0: iconst_0
     1: istore_1
     2: iinc 1, 1
     5: iload_1
     6: bipush 10
     8: if_icmplt 2
     11: return
```

最后再看看 for 循环：

```java
public class Demo3_6 {
    public static void main(String[] args) {
        for (int i = 0; i < 10; i++) {
        }
    }
}
```

字节码是：

```java
    0: iconst_0
    1: istore_1
    2: iload_1
    3: bipush 10
    5: if_icmpge 14
    8: iinc 1, 1
    11: goto 2
    14: return
```

> 比较 while 和 for 的字节码，你发现它们是一模一样的，殊途也能同归

## 2.6 练习-判断结果

```java
public class Demo3_6_1 {
    public static void main(String[] args) {
        int i = 0;
        int x = 0;
        while (i < 10) {
            x = x++;
            i++;
        }
        System.out.println(x); // 结果是 0
    }
}
```

```java
// 字节码指令如下：  
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=1
         0: iconst_0
         1: istore_1
         2: iconst_0
         3: istore_2
         4: goto          15
         7: iload_2							// 重点就是下面三行 一直在循环
         8: iinc          2, 1
        11: istore_2
        12: iinc          1, 1
        15: iload_1
        16: bipush        10
        18: if_icmplt     7
        21: getstatic     #16                 // Field java/lang/System.out:Ljava/io/PrintStream;
        24: iload_2
        25: invokevirtual #22                 // Method java/io/PrintStream.println:(I)V
        28: return

```

## 2.7 构造方法

### 2.7.1 \<cinit>()V

```java
public class Demo3 {
	static int i = 10;

	static {
		i = 20;
	}

	static {
		i = 30;
	}

	public static void main(String[] args) {
		System.out.println(i); //结果为30
	}
}
```

编译器会按从上至下的顺序，收集所有 static 静态代码块和静态成员赋值的代码，合并为一个特殊的方法`<cinit>()V` ：

```java
  static {};
    descriptor: ()V
    flags: ACC_STATIC
    Code:
      stack=1, locals=0, args_size=0
         0: bipush        10
         2: putstatic     #10                 // Field i:I		// 进行static变量的赋值
         5: bipush        20
         7: putstatic     #10                 // Field i:I
        10: bipush        30
        12: putstatic     #10                 // Field i:I
        15: return
```

`<cinit>()V` 方法会在类加载的初始化阶段被调用。

### 2.7.2 \<init>()V

```java
public class Demo4 {
	private String a = "s1";

	{
		b = 20;
	}

	private int b = 10;

	{
		a = "s2";
	}

	public Demo4(String a, int b) {
		this.a = a;
		this.b = b;
	}

	public static void main(String[] args) {
		Demo4 d = new Demo4("s3", 30);
		System.out.println(d.a);
		System.out.println(d.b);
	}
}
```

编译器会按从上至下的顺序，收集所有 {} 代码块和成员变量赋值的代码，形成新的构造方法，但原始构造方法内的代码总是在后：

```java
  public com.linxuan.Demo01(java.lang.String, int);
    descriptor: (Ljava/lang/String;I)V
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=3, args_size=3
         0: aload_0
         1: invokespecial #12                 // Method java/lang/Object."<init>":()V
         4: aload_0
         5: ldc           #15                 // String s1
         7: putfield      #17                 // Field a:Ljava/lang/String;
        10: aload_0
        11: bipush        20
        13: putfield      #19                 // Field b:I
        16: aload_0
        17: bipush        10
        19: putfield      #19                 // Field b:I
        22: aload_0
        23: ldc           #21                 // String s2
        25: putfield      #17                 // Field a:Ljava/lang/String;
        28: aload_0
        29: aload_1
        30: putfield      #17                 // Field a:Ljava/lang/String;
        33: aload_0
        34: iload_2
        35: putfield      #19                 // Field b:I
        38: return
      LineNumberTable:
        line 17: 0
        line 5: 4
        line 8: 10
        line 11: 16
        line 14: 22
        line 18: 28
        line 19: 33
        line 20: 38
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      39     0  this   Lcom/linxuan/Demo01;
            0      39     1     a   Ljava/lang/String;
            0      39     2     b   I
```

## 2.8 方法调用

```java
public class Demo01 {
    public Demo3_9() { }						// 构造方法  invokespecial指令
    
    private void test1() { }					// 私有方法  invokespecial指令
    
    private final void test2() { }				 // final方法	invokespecial指令
    
    public void test3() { }						// 普通成员方法 invokevirtual指令
    
    public static void test4() { }				// 静态方法   invokestatic指令
    
    public static void main(String[] args) {
        Demo01 demo01 = new Demo01();
        demo01.test1();
        demo01.test2();
        demo01.test3();
        demo01.test4();
        Demo01.test4();
    }
}
```

不同方法在调用时，对应的虚拟机指令有所区别

- 私有、构造、被final修饰的方法，在调用时都使用`invokespecial`指令
- 普通成员方法在调用时，使用`invokevirtual`指令。因为编译期间无法确定该方法的内容，只有在运行期间才能确定
- 静态方法在调用时使用`invokestatic`指令

```java
    0: new #2 					 // class class com/linxuan/Demo01
    3: dup
    4: invokespecial #3 		  // Method "<init>":()V
    7: astore_1
    8: aload_1
    9: invokespecial #4			 // Method test1:()V
    12: aload_1
    13: invokespecial #5 		 // Method test2:()V
    16: aload_1
    17: invokevirtual #6 		 // Method test3:()V
    20: aload_1
    21: pop
    22: invokestatic #7 		// Method test4:()V
    25: invokestatic #7 		// Method test4:()V
    28: return
```

- new 是创建【对象】，给对象分配堆内存，执行成功会将【对象引用】压入操作数栈
- dup 是赋值操作数栈栈顶的内容，本例即为【对象引用】，为什么需要两份引用呢，一个是要配合 invokespecial 调用该对象的构造方法 `"<init>":()V` （会消耗掉栈顶一个引用），另一个要配合 astore_1 赋值给局部变量
- 最终方法（final），私有方法（private），构造方法都是由 invokespecial 指令来调用，属于静态绑定
- 普通成员方法是由 invokevirtual 调用，属于动态绑定，即支持多态
- 成员方法与静态方法调用的另一个区别是，执行方法前是否需要【对象引用】
- 比较有意思的是 d.test4(); 是通过【对象引用】调用一个静态方法，可以看到在调用invokestatic 之前执行了 pop 指令，把【对象引用】从操作数栈弹掉了
- 还有一个执行 invokespecial 的情况是通过 super 调用父类方法

## 2.9 多态原理

<!--这里少些了很多-->

因为普通成员方法需要在运行时才能确定具体的内容，所以虚拟机需要调用**invokevirtual**指令

在执行invokevirtual指令时，经历了以下几个步骤

- 先通过栈帧中对象的引用找到对象
- 分析对象头，找到对象实际的Class
- Class结构中有**vtable**
- 查询vtable找到方法的具体地址
- 执行方法的字节码

HSDB

```java
java -cp ./lib/sa-jdi.jar sun.jvm.hotspot.HSDB		// 进入jdk安装目录
```

## 2.10 异常处理

### 2.10.1 try-catch

```java
public class Demo01 {
	public static void main(String[] args) {
		int i = 0;
		try {
			i = 10;
		}catch (Exception e) {
			i = 20;
		}
	}
}
```

```java
// 编译后部分字节码文件：
public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=3, args_size=1
         0: iconst_0
         1: istore_1
         2: bipush        10
         4: istore_1
         5: goto          12
         8: astore_2
         9: bipush        20
        11: istore_1
        12: return
      Exception table:		// 异常表 会检测2~5行的异常(不包含5行) 有异常会跳转至8行
         from    to  target type
             2     5     8   Class java/lang/Exception
      LineNumberTable:
        line 5: 0
        line 8: 2
        line 9: 5
        line 10: 9
        line 12: 12
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      13     0  args   [Ljava/lang/String;
            2      11     1     i   I
            9       3     2     e   Ljava/lang/Exception;
      StackMapTable: number_of_entries = 2
        frame_type = 255 /* full_frame */
          offset_delta = 8
          locals = [ class "[Ljava/lang/String;", int ]
          stack = [ class java/lang/Exception ]
        frame_type = 3 /* same */
```

- 可以看到多出来一个 Exception table 的结构，[from, to) 是**前闭后开**（也就是检测2~4行）的检测范围，一旦这个范围内的字节码执行出现异常，则通过 type 匹配异常类型，如果一致，进入 target 所指示行号
- 8行的字节码指令 astore_2 是将异常对象引用存入局部变量表的2号位置（为e）

### 2.10.2 多个single-catch

```java
public class Demo01 {
	public static void main(String[] args) {
		int i = 0;

		try {
			i = 10;
		} catch (ArithmeticException e) {
			i = 20;
		} catch (NullPointerException e) {
			i = 30;
		} catch (Exception e) {
			i = 40;
		}
	}
}
```

```java
  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=3, args_size=1
         0: iconst_0
         1: istore_1
         2: bipush        10
         4: istore_1
         5: goto          26
         8: astore_2
         9: bipush        20
        11: istore_1
        12: goto          26
        15: astore_2
        16: bipush        30
        18: istore_1
        19: goto          26
        22: astore_2
        23: bipush        40
        25: istore_1
        26: return
      Exception table:
         from    to  target type
             2     5     8   Class java/lang/ArithmeticException
             2     5    15   Class java/lang/NullPointerException
             2     5    22   Class java/lang/Exception
      LineNumberTable:...
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      27     0  args   [Ljava/lang/String;
            2      25     1     i   I
            9       3     2     e   Ljava/lang/ArithmeticException;
           16       3     2     e   Ljava/lang/NullPointerException;
           23       3     2     e   Ljava/lang/Exception;
```

- 因为异常出现时，只能进入 Exception table 中一个分支，所以局部变量表 slot 2 位置被共用

### 2.10.3 multi-catch 的情况

```java
public class Demo01 {
	public static void main(String[] args) {
		try {
			Method test = Demo01.class.getMethod("test");
			test.invoke(null);
		} catch (NoSuchMethodException | IllegalAccessException | InvocationTargetException e) {
			e.printStackTrace();
		}
	}
	public static void test() {
		System.out.println("ok");
	}
}
```

```java
  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=2, args_size=1
         0: ldc           #1                  // class com/linxuan/demo01/Demo01
         2: ldc           #16                 // String test
         4: iconst_0
         5: anewarray     #18                 // class java/lang/Class
         8: invokevirtual #20                 // Method java/lang/Class.getMethod:(Ljava/lang/String;[Ljava/lang/Class;)Ljava/lang/reflect/Method;
        11: astore_1
        12: aload_1
        13: aconst_null
        14: iconst_0
        15: anewarray     #3                  // class java/lang/Object
        18: invokevirtual #24                 // Method java/lang/reflect/Method.invoke:(Ljava/lang/Object;[Ljava/lang/Object;)Ljava/lang/Object;
        21: pop
        22: goto          30
        25: astore_1
        26: aload_1
        27: invokevirtual #30                 // Method java/lang/ReflectiveOperationException.printStackTrace:()V
        30: return
      Exception table:
         from    to  target type
             0    22    25   Class java/lang/NoSuchMethodException
             0    22    25   Class java/lang/IllegalAccessException
             0    22    25   Class java/lang/reflect/InvocationTargetException
      LineNumberTable:...
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      31     0  args   [Ljava/lang/String;
           12      10     1  test   Ljava/lang/reflect/Method;
           26       4     1     e   Ljava/lang/ReflectiveOperationException;
```

### 2.10.4 finally

```java
public class Demo01 {
	public static void main(String[] args) {
		int i = 0;
		try {
			i = 10;
		} catch (Exception e) {
			i = 20;
		} finally {
			i = 30;
		}
	}
}
```

```java
  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=4, args_size=1
         0: iconst_0
         1: istore_1
         2: bipush        10			// try代码块
         4: istore_1
         5: goto          24
         8: astore_2					// catch代码块
         9: bipush        20			
        11: istore_1
        12: bipush        30			// 执行finally代码块
        14: istore_1
        15: goto          27
        18: astore_3					// catch其余的异常类型
        19: bipush        30			// 执行finally代码块
        21: istore_1
        22: aload_3
        23: athrow
        24: bipush        30			// 执行finally代码块
        26: istore_1
        27: return
      Exception table:
         from    to  target type
             2     5     8   Class java/lang/Exception
             2    12    18   any
      LineNumberTable:...
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      28     0  args   [Ljava/lang/String;
            2      26     1     i   I
            9       3     2     e   Ljava/lang/Exception;
```

可以看到 ﬁnally 中的代码被复制了 3 份，分别放入 try 流程，catch 流程以及 catch剩余的异常类型流程

虽然从字节码指令看来，每个块中都有finally块，但是finally块中的代码只会被执行一次

### 2.10.5 finally中的return

```java
public class Demo3 {
	public static void main(String[] args) {
		int i = Demo3.test();
        //结果为20
		System.out.println(i);
	}

	public static int test() {
		int i;
		try {
			i = 10;
			return i;
		} finally {
			i = 20;
			return i;
		}
	}
}
```

```java
Code:
     stack=1, locals=3, args_size=0
        0: bipush        10
        2: istore_0
        3: iload_0
        4: istore_1 				 //暂存返回值
        5: bipush        20
        7: istore_0
        8: iload_0
        9: ireturn					//ireturn会返回操作数栈顶的整型值20
       10: astore_2					//如果出现异常，还是会执行finally块中的内容，没有抛出异常
       11: bipush        20
       13: istore_0
       14: iload_0
       15: ireturn					//这里没有athrow了
     Exception table:
        from    to  target type
            0     5    10   anyCopy
```

- 由于 ﬁnally 中的 ireturn 被插入了所有可能的流程，因此返回结果肯定以ﬁnally的为准
- 至于字节码中第 2 行，似乎没啥用，且留个伏笔，看下个例子
- 跟上例中的 ﬁnally 相比，发现没有 athrow 了，这告诉我们：如果在 ﬁnally 中出现了 return，会吞掉异常
- 所以不要在finally中进行返回操作

例如下面的操作会吞掉异常：

```java
public class Demo3 {
   public static void main(String[] args) {
      int i = Demo3.test();
      //最终结果为20
      System.out.println(i);
   }

   public static int test() {
      int i;
      try {
         i = 10;
         i = i/0;			//这里应该会抛出异常 但是并没有
         return i;
      } finally {
         i = 20;
         return i;
      }
   }
}
```

会发现打印结果为20，并未抛出异常

### 2.10.6 finally不带return

```java
public class Demo4 {
	public static void main(String[] args) {
		int i = Demo4.test();
		System.out.println(i);	// i = 10
	}

	public static int test() {
		int i = 10;
		try {
			return i;
		} finally {
			i = 20;
		}
	}
}
```

```java
Code:
     stack=1, locals=3, args_size=0
        0: bipush        10
        2: istore_0 						// 赋值给i 10
        3: iload_0							// 加载到操作数栈顶
        4: istore_1 						// 加载到局部变量表的1号位置
        5: bipush        20
        7: istore_0 						// 赋值给i 20
        8: iload_1							// 加载局部变量表1号位置的数10到操作数栈
        9: ireturn 							// 返回操作数栈顶元素 10
       10: astore_2
       11: bipush        20
       13: istore_0
       14: aload_2 							// 加载异常
       15: athrow 							// 抛出异常
     Exception table:
        from    to  target type
            3     5    10   anyCopy
```

## 2.8 Synchronized

```java
public class Demo01 {
	public static void main(String[] args) {
		Object lock = new Object();
		synchronized (lock) {
			System.out.println("ok");
		}
	}
}
```

```java
  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=1
         0: new           #3                  // class java/lang/Object
         3: dup
         4: invokespecial #8                  // Method java/lang/Object."<init>":()V
         7: astore_1
         8: aload_1
         9: dup
        10: astore_2
        11: monitorenter
        12: getstatic     #16                 // Field java/lang/System.out:Ljava/io/PrintStream;   
        15: ldc           #22                 // String ok
        17: invokevirtual #24                 // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        20: aload_2
        21: monitorexit
        22: goto          28
        25: aload_2
        26: monitorexit
        27: athrow
        28: return
      Exception table:
         from    to  target type
            12    22    25   any
            25    27    25   any
      LineNumberTable:...
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      29     0  args   [Ljava/lang/String;
            8      21     1  lock   Ljava/lang/Object;
```

> 方法级别的 synchronized 不会在字节码指令中有所体现

